#!/usr/local/bin/perl
use 5.008;
use strict;
use warnings;
# PODNAME: pod2markdown
# ABSTRACT: Convert POD text to Markdown

use Pod::Markdown;
use Term::Encoding ();
use Getopt::Long;
use Pod::Usage;

my %opts;
GetOptions(\%opts, qw(
  help|h
  encoding|e=s
  match_encoding|match-encoding
)) or pod2usage(2);
pod2usage(1) if $opts{help};

# TODO: Pod::Simple::parse_from_file(@ARGV[0,1]);

my $in_fh  = get_handle(shift(@ARGV), '<', \*STDIN);
my $out_fh = get_handle(shift(@ARGV), '>', \*STDOUT);

convert($in_fh, $out_fh);

sub convert {
    my ($in_file, $out_file) = @_;

    my $enc = $opts{encoding} || Term::Encoding::get_encoding();

    my $parser = Pod::Markdown->new;
    $parser->output_encoding($enc) if $enc;
    $parser->match_encoding(1) if $opts{match_encoding};
    $parser->output_fh($out_file);
    $parser->parse_file($in_file);
}

sub get_handle {
  my ($path, $op, $default) = @_;
  (!defined($path) || $path eq '-') ? $default : do {
    open(my $fh, $op, $path)
      or die "Failed to open '$path': $!\n";
    $fh;
  };
}

=head1 SYNOPSIS

    # parse STDIN, print to STDOUT
    $ pod2markdown < POD_File > Markdown_File

    # parse file, print to STDOUT
    $ pod2markdown input.pod

    # parse file, print to file
    $ pod2markdown input.pod output.mkdn

    # parse STDIN, print to file
    $ pod2markdown - output.mkdn

=head1 DESCRIPTION

This program uses L<Pod::Markdown> to convert POD into Markdown sources.

It accepts two optional arguments:

=for :list
* input pod file (defaults to C<STDIN>)
* output markdown file (defaults to C<STDOUT>)

=head1 OPTIONS

=over

=item --encoding

Specify the encoding for the output file.

=item --match-encoding

Use the same C<=encoding> as the input pod for the output file.

=back

=head1 SEE ALSO

This program is strongly based on C<pod2mdwn> from L<Module::Build::IkiWiki>.
